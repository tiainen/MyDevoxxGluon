buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.10'
    }
}

apply plugin: 'org.javafxports.jfxmobile'

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
}

mainClassName = 'com.devoxx.DevoxxApplication'
preloaderClassName = 'com.devoxx.DevoxxPreloader'

// For desktop (for mobile check java.custom.properties)
applicationDefaultJvmArgs = [
        "-Djavafx.pulseLogger=false",
        "-Denable.logging=true",
        "-Dfile.encoding=UTF-8"
]

dependencies {
    compile project(':DevoxxClientCommon')
    compile 'com.gluonhq:glisten-afterburner:1.2.2'

    compile 'com.gluonhq:charm-glisten-connect-view:4.4.4'
    compile 'com.gluonhq:maps:1.0.1'

    androidCompile 'com.google.android.gms:play-services-wearable:9.4.0'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

jfxmobile {
    downConfig {
        version charm_down_version
        plugins 'barcode-scan', 'browser', 'cache', 'connectivity', 'device', 'display', 'lifecycle',
                'local-notifications', 'push-notifications', 'runtime-args', 'settings', 'share', 'statusbar', 'storage', 'video'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
        signingConfig {
            storeFile project.hasProperty('DEVOXX_APK_SIGN_KEYSTORE') ? file(DEVOXX_APK_SIGN_KEYSTORE) : null
            storePassword project.hasProperty('DEVOXX_APK_SIGN_STOREPASSWORD') ? DEVOXX_APK_SIGN_STOREPASSWORD : ''
            keyAlias project.hasProperty('DEVOXX_APK_SIGN_KEYALIAS') ? DEVOXX_APK_SIGN_KEYALIAS : ''
            keyPassword project.hasProperty('DEVOXX_APK_SIGN_KEYPASSWORD') ? DEVOXX_APK_SIGN_KEYPASSWORD : ''
        }
    }
    ios {
        arch = 'arm64'
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.devoxx.**.*',
                'com.gluonhq.**.*',
                'javax.xml.stream.*.',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}

task printf() << {
    configurations.runtime.each {
        println it
        (it.name.endsWith('jar') ? zipTree(it) : fileTree(it)).each {
            println "\t$it"
        }
    }
}

project.afterEvaluate {
    explodeAarDependencies(project.configurations.androidCompile)
    androidRelease.dependsOn copyWearableAPK
}

task copyWearableAPK(type: Copy) {
    description = 'Copying wearable apk into res/raw'
    from project(':DevoxxClientWearable').file('build/javafxports/android/')
    into project(':DevoxxClient').file('src/android/res/raw')
    include 'DevoxxClientWearable.apk'
    rename ('DevoxxClientWearable.apk', 'devoxx_client_wearable.apk')
}

// Note: Run androidRelease task for the client project only, and then install on mobile
tasks.copyWearableAPK.dependsOn ':DevoxxClientWearable:androidRelease'

